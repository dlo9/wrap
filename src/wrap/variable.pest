escape = { "\\" }
tilde = { "~" }
expansion_indicator = { "$" }

escapable = { (escape | tilde | expansion_indicator) }
escaped = { escape ~ escapable }
not_escaped = { !escapable ~ ANY }
literal = { escaped | not_escaped }

// Variables: $HOME or ${USER}
variable_identifier = { (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_" )* }

// Postitional arguments: $1, ${2}, but not $0
positional_identifier = { ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }

// All positionals: $@
positionals_identifier = { "@" }

expansion_name = {
                    (variable_identifier | ("{" ~ variable_identifier ~ "}")) |
                    (positional_identifier | ("{" ~ positional_identifier ~ "}")) |
                    (positionals_identifier | ("{" ~ positionals_identifier ~ "}"))
                 }

expansion = { tilde | (expansion_indicator ~ (expansion_name | ("{" ~ expansion_name ~ "}"))) }

argument = { SOI ~ (literal | expansion)* ~ EOI }
